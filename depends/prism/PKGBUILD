pkgname=prismmodelchecker-git
pkgver=4.6.r128.ged7a20c6
pkgrel=1
pkgdesc="Probabilistic Symbolic Model Checker"
arch=('any')
url="https://www.prismmodelchecker.org/"
license=('GPL2')
depends=('java-runtime>=9')
makedepends=('git' 'java-environment>=9')
checkdepends=('python')
source=("$pkgname::git+https://github.com/prismmodelchecker/prism#commit=ed7a20c6930400fbdfff0383c3e4a12a3952f124")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$pkgname"
    if git describe --long --tags >/dev/null 2>&1; then
        git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
    else
        printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git describe --always)"
    fi
}

build() {
    cd "$srcdir/$pkgname/prism"
    make
    make binary
}

check() {
    cd "$srcdir/$pkgname/prism"
    make test
}

package() {
    cd "$srcdir/$pkgname/prism"
    sed -i 's/^\(PRISM_DIR=\).*/\1"\/usr\/share\/java\/prism\/"/' bin/prism
    sed -i 's/^\(PRISM_DIR=\).*/\1"\/usr\/share\/java\/prism\/"/' bin/xprism

    mkdir -p $pkgdir/usr/share/java/prism/
    cp -r bin/ $pkgdir/usr/share/java/prism/bin/
    cp -r lib/ $pkgdir/usr/share/java/prism/lib/
    cp -r etc/ $pkgdir/usr/share/java/prism/etc/

    install -d $pkgdir/usr/bin/
    ln -s "/usr/share/java/prism/bin/prism" "${pkgdir}/usr/bin/prism"
    ln -s "/usr/share/java/prism/bin/xprism" "${pkgdir}/usr/bin/xprism"
    ln -s "/usr/share/java/prism/bin/ngprism" "${pkgdir}/usr/bin/ngprism"
    ln -s "/usr/share/java/prism/etc/scripts/prism-auto" "${pkgdir}/usr/bin/prism-auto"
}

# vim: set sw=4 ts=4 et:
